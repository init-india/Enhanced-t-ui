plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace "ohi.andre.consolelauncher"
    compileSdkVersion 33
    buildToolsVersion "33.0.2"

    defaultConfig {
        applicationId "ohi.andre.consolelauncher"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 205
        versionName "v6.15"
        flavorDimensions "default"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
        debug {
            minifyEnabled false
            shrinkResources false
        }
    }

    productFlavors {
        fdroid { }
        playstore { }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lint {
        checkReleaseBuilds false
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def formattedDate = new Date().format('HHmmss')
            outputFileName = "${variant.name}-${variant.versionName}-${formattedDate}.apk"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.10"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core:1.10.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'it.andreuzzi:CompareString2:1.0.9'
    implementation 'org.jsoup:jsoup:1.15.4'

    playstoreImplementation 'net.sourceforge.htmlcleaner:htmlcleaner:2.2.0'
    playstoreImplementation 'com.jayway.jsonpath:json-path:2.4.0'
    playstoreImplementation 'com.google.api-client:google-api-client:1.34.0'
    playstoreImplementation 'com.google.android.gms:play-services-maps:18.1.0'
    playstoreImplementation 'androidx.work:work-runtime:2.8.1'
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:1.8.10"
        force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.10"
        force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.10"
    }
}
